apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../dependency_versions.gradle'

android {
    compileSdkVersion compileSdkVersionSet
    defaultConfig {
        applicationId "com.tiago.tflcodingchallenge"
        minSdkVersion minSdkVersionSet
        targetSdkVersion targetSdkVersionSet
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        //Setup API variables

        def tflApiPropertiesFile = rootProject.file("tflapi.properties")
        def tflApiProperties = new Properties()
        tflApiProperties.load(new FileInputStream(tflApiPropertiesFile))

        buildConfigField "String", "TFL_SERVICE_BASE_URL", tflApiProperties['base_url']
        buildConfigField "String", "TFL_SERVICE_APP_ID", tflApiProperties['app_id']
        buildConfigField "String", "TFL_SERVICE_APP_KEY", tflApiProperties['app_key']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation project(':entities')
    implementation project(':usecases')
    implementation project(':tflservice')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    //Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    //Android X
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintVersion"
    //MVVM
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"


    //Image loading
    implementation "com.squareup.picasso:picasso:$picassoVersion"

    //Testing
    testImplementation project(':testdata')
    testImplementation "androidx.arch.core:core-testing:$archCoreTestingVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"

    androidTestImplementation "androidx.test.ext:junit:$testExtJunitVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    androidTestImplementation "com.schibsted.spain:barista:$baristaVersion"
}
